options {
	LOOKAHEAD=1; //melhorar para 1
	MULTI=true;
}
 
PARSER_BEGIN(yal2jvm)

import java.io.FileInputStream;
import java.io.File;
import java.io.IOException;
import java.io.FileNotFoundException;

public class yal2jvm {
	static java.util.Stack argStack;
	public static void main(String[] args) throws ParseException{
		argStack = new java.util.Stack(); 

		File filename = new File(args[0]);
		try { 
			yal2jvm parser = new yal2jvm(new FileInputStream(filename));
			SimpleNode root = parser.Module();
			
			root.dump("");
			
			SymbolTable symbolTable = new SymbolTable();

			System.out.println("\n\nCommencing Semantic Analysis.");
			System.out.println("Checking Operations.\n");
			
			symbolTable.fillSymbols(root);

			System.out.println("Checking Calls.\n");
			
			if (!symbolTable.hasErrors()) {
				System.out.println("\n\nCommencing Code Generation.\n\n");

				String filenameWthExt = filename.getName().replaceFirst("[.][^.]+$","");

				Sampler sampler = new Sampler("codeGenerated/" + filenameWthExt + ".j");
				Generator generator = new Generator(sampler, symbolTable);
			
				ASTModule rootModule = (ASTModule) root;
				rootModule.generatorVisit(generator);
			}
			System.out.println("\n Code Generation Done. \n");

		} catch(FileNotFoundException e) {
			System.out.println("Exception found");
		}
		
	}
}

PARSER_END(yal2jvm)

SKIP : {
	" "	| "\t"	| "\n" | "\r" | 
	<"//" (~["\n", "\r"])* ("\n" | "\r" | "\r\n")> | 
	<"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/"> 
}

/* Reserved words */
TOKEN: {
	<RELA_OP : ">" | "<" | "<=" | ">=" | "==" | "!=" >
	| <ADDSUB_OP: "+" | "-">
	| <ARITH_OP: "*" | "/" | "<<" | ">>" | ">>>" >
	| <BITWISE_OP: "&" | "|" | "^" >
	| <NOT_OP: "!" >
	| <WHILE: "while">
	| <IF: "if" >
	| <ELSE: "else">
	| <ASSIGN: "=">
	| <ASPA: "\"">
	| <LPAR: "(" >
	| <RPAR: ")" >
	| <VIRG: "," >
	| <PVIRG: ";" >
	| <DOT: "." >
	| <LCHAVETA: "{" >
	| <RCHAVETA: "}" >
	| <FUNCTION: "function" >
	| <MODULE: "module" >
	| <SIZE: "size">
}

TOKEN: {
	<INTEGER: (<DIGIT>)+>
	| <ID: <LETTER> (<LETTER> | <DIGIT>)*>
	| <#LETTER: ["$", "A"-"Z","_","a"-"z"]>
	| <#DIGIT: ["0"-"9"]>
	| <STRING: "\"" (["a"-"z","A"-"Z","0"-"9",":"," ","="])+ "\"">
}

// MAIN BLOCKS OF INFORMATION 

/**
 * Main Module
*/
SimpleNode Module(): {Token moduleName;} {
	try {
		<MODULE> moduleName=<ID> 

		{jjtThis.name = moduleName.image; }

		<LCHAVETA>
	} catch (ParseException e) {
		System.out.println("Error on module start, with Exception thrown " + e.toString());
		Token t = getToken(0);
		try {
			if (t.kind == LCHAVETA) 
				getNextToken();
			else
				error_skipto(PVIRG);
		} catch (ParseException e2) {
			System.out.println("Error on module");
		}
	}
	
	(Declaration())*
	(Function())*
		
	try {
		< RCHAVETA > { return jjtThis; }
	} catch (ParseException e) {
		System.out.println("Error on module end, with Exception thrown " + e.toString());
		return jjtThis;	
	}
}

/**
 * Function, where the file's statement list lies.
*/
void Function(): {Token name;} {
	try {
	 	<FUNCTION>  name=<ID> {jjtThis.functionName = name.image; jjtThis.returnType = Utils.VOID; } (
	     	( {jjtThis.returnType = Utils.SCALAR; jjtThis.returnValue = name.image; }
				 (ArrayElement(name) {jjtThis.returnType = Utils.ARRAY; })? <ASSIGN> name=<ID> {jjtThis.functionName = name.image;})?
		     (VarList()) 
		)	
	} catch(ParseException e) {
		System.out.println("Error on function statement, with Exception thrown " + e.toString());
		try {
			error_skipto(PVIRG);
		} catch (ParseException e2) {
			System.out.println("Error on function");
		}
	} 
	
	Stmtlst() 
} 

/** 
 * Declaration, handling definitions and assignment of variables outside functions.
*/
void Declaration(): {String declaration; Token tmp; Token tmp2;} {
	
	try {	
		tmp=<ID>  
		 (ArrayElement(tmp)  {jjtThis.setType(Utils.DECLARATION_ARRAY);})?  {jjtThis.value = tmp.image;}
		(< ASSIGN > ((ArraySize()  {jjtThis.setType(Utils.DECLARATION_ARRAY);} ) | (tmp=< ADDSUB_OP > 
			{jjtThis.setOperator(tmp.image);})? tmp2=< INTEGER >{jjtThis.setAssigned(tmp2.image); jjtThis.setType(Utils.DECLARATION_SCALAR);}))?
			 < PVIRG >
	}
	catch(ParseException e) {
		System.out.println("Error on declaration, with Exception thrown " + e.toString());

		error_skipto(PVIRG);
	}
}

/**
 * VarList, the set of variables of a specific function.
*/
void VarList(): {String variables; Token tmp;} { 
	try {
		<LPAR>
	} catch (ParseException e) {
		System.out.println("Error on argument list, no opening parenthesis.");
		System.out.println("Specific Exception thrown:" + e.toString());
	}
	try {
		
		(Var() 
			(< VIRG > Var()
			)*	
		)? 

		<RPAR>
	} catch (ParseException e) {
		System.out.println("Error on argument list variables or closing parenthesis.");
		System.out.println("Specific Exception thrown: "+ e.toString());
		 

		error_skipto_unless(PVIRG, RPAR);
	}
}

/**
 * StmtList, the set of all statements inside a function. Opening and 
 * closing brackets are inside this list, to better detect and isolate 
 * exceptions thrown.
*/
void Stmtlst() #void: {} {
	try {
		<LCHAVETA>
	} 
	catch (ParseException e) {
		System.out.println("Error on statement list, opening bracket not detected."
			+"\nSpecific exception: " + e.toString());
		error_skipto_withoutconsuming(ID);
		return;
	}

	(Stmt())* 
	
	try {
		<RCHAVETA>
	} catch (ParseException e) {
		System.out.println("Error on statement list, closing bracket not detected."
			+"\nSpecific exception: " + e.toString());
		error_skipto(ID);
	}
}

// STATEMENT INFORMATION

/**
 * Statement, the main code unit inside a function. It includes the assign
 * of variables, the call of functions, if/else blocks and while loops.
*/
void Stmt() #void: {Token t;} {
	try {
		
		While() | If() | (t=<ID> (LOOKAHEAD(2) Call(t) | Assign(t))) < PVIRG >

	} catch (ParseException e) {
		System.out.println("Error on a statement, maybe missing ';'\nSpecific Exception thrown: ." 
		+ e.toString());
		error_skipto(PVIRG);
	}
}

/**
 * Call, where a method is invoked.
 * @param Token t contains ID information for printing.
*/
void Call(Token t): {String string; Token tmp;} {
	{jjtThis.value = t.image; jjtThis.setType(Utils.CALL); jjtThis.calledFunction = t.image;}
	try {
		( < DOT > tmp=<ID > 
		{jjtThis.setType(Utils.EXTERNAL_CALL); jjtThis.packageName = (String) jjtThis.value; jjtThis.calledFunction = tmp.image;})? 
		 < LPAR > (ArgumentList() ) < RPAR >
		
	} catch (ParseException e) {
		System.out.println("Error on call."
			+"\nSpecific exception: " + e.toString());
		error_skipto_unless(PVIRG, LCHAVETA);
	}
}

/**
 * Assign to handle all the assignment of variables, be they scalar or arrays.
 * @param Token t contains ID information for printing.
*/
void Assign(Token t): {String string;} {
	try {
		LhsNoID(t)
		<ASSIGN> 
		Rhs()
	} catch (ParseException e) {
		System.out.println("Error on assign."
			+"\nSpecific exception: " + e.toString());
		
		error_skipto(PVIRG);
	}
}

/**
 * While loop.
*/
void While(): {Token t; String tmp; } {
	try { 
		< WHILE > Exprtest() Stmtlst() 
	} catch (ParseException e) {
		System.out.println("Error on while");
	}
}

/**
 * If block.
*/
void If(): { String tmp; } {
	try {
		< IF > Exprtest() Stmtlst() (Else())?
	} catch (ParseException e) {
		System.out.println("Error on if");
	}	
}

/**
 * Else block, may or may not exist after if block.
*/
void Else(): {} {
	try {
		< ELSE > Stmtlst()
	}
	catch (ParseException e) {
		System.out.println("Error on else");
		error_skipto(PVIRG);
	}
}

/**
 * Lhs, the left side of a two-sided operation, such as a comparison or assigment.
 * @return String containing information of lhs block.
*/
void Lhs() #void: {Token t;} {
	t=<ID>

	(ArrayAccess(t) | ScalarAccess(t)) 	
}

/**
 * LhsNoID, particular use of Lhs, specific for a function to remove ambiguity 
 * while maintaining readability.
 * @return String containing information of lhs block, excluding <ID>.
*/
void LhsNoID(Token t) #void: {String tmp; } {
	(ArrayAccess(t) | ScalarAccess(t)) 
}

/**
 * Rhs, the right side of a two-sided operation, such as a comparison or assigment.
 * @return String containing information of right block.
*/
void Rhs() : {Token t; String tmp;} {
	try {
	(
		(
		Term()((t=<ARITH_OP > | t=<BITWISE_OP> | t=< ADDSUB_OP > | t=<RELA_OP>) Term() 
			{jjtThis.setType(Utils.OP); jjtThis.jjtSetValue(t.image);})?
		) 
		|
		 ( ArrayInstantion()  )
	)
	 
	/*
	(
		(
		string=Term()((tmp=< ARITH_OP > | tmp=< BITWISE_OP > | tmp=< ADDSUB_OP >) tmpString=Term() {string += " " + tmp.image + " " + tmpString;})?
		) 
		|
		 ("[" tmpString=ArraySize() "]" {string="["+tmpString+"]";})
	)
		{return string;} 
	*/

	}
	catch (ParseException e) {
		System.out.println("Error on rhs.\nSpecific Exception thrown: " + e.toString());
		error_skipto_withoutconsuming(PVIRG);
	}
	
}

/**
 * Term, a subset of a Rhs.
*/
void Term(): {Token t;} {
	try {
		( (t=<ADDSUB_OP> | t=<RELA_OP> )
			{jjtThis.setOperator(t.image);} )? 

		( (t=<INTEGER> {jjtThis.setType(Utils.NUMBER); jjtThis.value = t.image;}) 
			|
		 ( t=<ID> ( (ArrayAccess(t)) 
		  	| 
			(
				LOOKAHEAD(2) Call(t) | (ScalarAccess(t))
			)
			)
		 	)
		 )
	} catch (ParseException e) {
		System.out.println("Error on term.\nSpecific Exception: " + e.toString());
		error_skipto_withoutconsuming(PVIRG);
	}
} 

/**
 * Exprtest, handles the small comparison block, present in the while and if blocks.
 * @return String containing information of right block.
*/
void Exprtest(): {Token t; } {	
	try {
		< LPAR > Lhs() 
	} catch (ParseException e) {
		System.out.println("Invalid opening condition for structure. \nSpecific Exception thrown: "
		 + e.toString());
		error_skipto_withoutconsuming(RELA_OP);
	}
	try {
		t=< RELA_OP > {jjtThis.value = t.image;}
		Rhs()  <RPAR> 
	}
	catch (ParseException e) {
		System.out.println("Invalid comparison operation or ending condition for structure");
		System.out.println("Specific exception thrown: " + e.toString());
		 
		error_skipto_unless(PVIRG, RPAR);
	}
}

// ARGUMENTS

/**
 * Argument List, storing the information of the arguments passed inside a call, for example.
*/
void ArgumentList(): {} {
	try {

	(Argument() (< VIRG > Argument() )*)?

	} catch (ParseException e) {

		System.out.println("Invalid argument. \nSpecific Exception thrown: " + e.toString());
		error_skipto(RPAR);

	}
}

/**
 * Argument, could be an ID, String or integer. 
 * @return String containing information of argument.
*/
void Argument() : {Token t; } {
	 (t=<ID> | t=<STRING> | t=<INTEGER> {jjtThis.type = Utils.NUMBER;} ){jjtThis.content = t.image;}
}

// ACCESS

/**
 * Array Size. Contains the information of the size of an array
 * @return String with the information of the array size.
*/
void ArraySize() #void: { Token tmp; Token tmp2;} {
	(LOOKAHEAD(2) ArrayInstantion() | ("[" tmp=<ID> ScalarAccess(tmp) "]"))
}

/**
 * Array Access, accessing information of an array.
 * @return String containing information of array access, specifically the index..
*/
void ArrayAccess(Token content) :  {String index;} {
	"[" index=Index() "]"
	{jjtThis.content = content.image; jjtThis.index = index; }
}

void ArrayInstantion() : {String size;} {
	"[" size=Index() "]"
	{ jjtThis.size = size; }
}

/**
 * Scalar Access.
 * @return String containing information of size, or nothing if no '.' is found.
*/
void ScalarAccess(Token content) :  {Token size; } {
	{jjtThis.value = content.image;} (<DOT > size=< SIZE > {jjtThis.setType(Utils.SIZE);})? 
}

/**
 * Index of an array.
 * @return String with index.
*/
String Index() #void: {Token value;} {
	(value=< ID > | value=< INTEGER >)
	{return value.image;}
}

void Var() : {Token t; String tmp;} {
	t=<ID> {jjtThis.value = t.image; jjtThis.setType(Utils.SCALAR); } 
	
	(ArrayElement(t) {jjtThis.value = null; jjtThis.setType(Utils.ARRAY); })?
}

/**
 * Array Element, checks to see if the element is correctly formatted, important for
 * error recovery.
*/
void ArrayElement(Token t): { } {
	"[" "]" {jjtThis.value = t.image;}
}

// ERROR RECOVERY

/**
 * Skips all tokens until a specific character is found.
*/
JAVACODE
void error_skipto(int kind) {
  Token t;
  do {
    t = getNextToken();
  } while (t.kind != kind);
}


/**
 * Skips all tokens until a specific character is found, or another more important
 * but less likely token appears unexpectedly.
*/
JAVACODE
void error_skipto_unless(int kind, int stopKind) {
  Token t;
  do {
    t = getNextToken();
  } while (t.kind != kind && t.kind != stopKind);
}

/**
 * Skips all tokens until a specific character, without consuming that specific token.
*/
JAVACODE
void error_skipto_withoutconsuming(int kind) {
  Token t;

  boolean hasReached = false;
  while (!hasReached) {
  	t = getToken(1);
  	if (t.kind == kind)
  		hasReached = true;
  	else 
  		getNextToken();
  }
}

